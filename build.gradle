plugins {
    id 'org.springframework.boot' version '2.7.0-SNAPSHOT'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'jacoco'
    id 'checkstyle'
    id 'com.diffplug.spotless' version '6.20.0'
}

group = 'johnny.tutorial'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'
targetCompatibility = '11'

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
    maven { url 'https://repo.spring.io/snapshot' }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-devtools'
    implementation 'com.h2database:h2'
    implementation 'org.postgresql:postgresql'
    implementation 'org.apache.commons:commons-dbcp2'
    implementation 'org.springdoc:springdoc-openapi-ui:1.6.4'
    implementation "org.springframework.boot:spring-boot-starter-hateoas"
    implementation "org.flywaydb:flyway-core"

    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'

    testCompileOnly 'org.projectlombok:lombok:1.18.22'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.mockito:mockito-inline:5.2.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.+'
}

bootRun {
    mainClass = 'johnny.gamestore.springboot.GameStoreApplication'
    jvmArgs = ["-XX:MaxMetaspaceSize=480m"]
    environment = ["DATABASE_URL": "postgres://postgres:pw@localhost:5432/postgres" ]
    systemProperty "server.port", "8080"
    systemProperty "spring.profiles.active", "local"
}

checkstyle {
    configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
}

checkstyleMain {
    source ='src/main/java'
}
checkstyleTest {
    source ='src/test/java'
}

spotless {
    java {
        googleJavaFormat()
        importOrder 'lombok', 'johnny', 'io', 'org', 'java', 'javax', ''
        removeUnusedImports()
        target 'src/**/*.java'
        trimTrailingWhitespace()
        endWithNewline()

        // Remove blank lines between imports
        replaceRegex 'collapse import blank lines', '(?<=import .+;\n)\n+(?=import )', ''
    }
}

jacoco {
    toolVersion = "0.8.11"
}

test {
    useJUnitPlatform()

    filter {
        // exclude all tests containing the `integration` file path or package.
        excludeTestsMatching "*.integration.*"
    }

    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        xml.outputLocation = layout.buildDirectory.file("reports/jacoco/test/jacocoTestReport.xml")
        html.required = true
    }
}
